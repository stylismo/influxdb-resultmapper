buildscript {
    ext.kotlin_version = '1.2.41'
    ext.influxdb_client_version = '2.10'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.9"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
    }
}

group 'org.mybop'
version '1.0-UNSTABLE4'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.influxdb:influxdb-java:$influxdb_client_version"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.18.3'
    testImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.5.0'
    testImplementation 'org.assertj:assertj-core:3.10.0'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
        javaParameters = true
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
        javaParameters = true
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-parameters'
}

dokka {
    moduleName = 'ormlite-rx'
    outputFormat = 'html'

    jdkVersion = 8
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
}

bintray {
    user = bintrayUser
    key = bintrayKey

    publications = ['mavenJava']

    publish = true

    pkg {
        repo = 'maven'
        name = project.name
        licenses = ['ISC']
        vcsUrl = 'https://github.com/GautierLevert/influxdb-resultmapper'
        version {
            name = project.version
            desc = 'Result Mapper for InfluxDb queries'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}
